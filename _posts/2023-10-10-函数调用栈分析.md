
本文结合一个具体的示例程序分析了函数调用时栈的变化细节。首先介绍一些惯例: 函数调用时通过栈来实现的，通常用``rbp``寄存器来保存正在运行函栈帧的开始地址，用``rsp``寄存器来保存栈顶的地址，用``rip``寄存器来保存CPU将要执行指令的地址，用``eax``寄存器来保存函数调用的返回值。

测试程序如下:
```cpp
#include <stdio.h>

int sum(int a, int b){
    int s = a + b;
    return s;
}

int main(int argc, char* argv[]){

    int n = sum(1,2);
    printf("n : %d\n" ,n);
    return 0;
}
```
编译命令如下:
```bash
g++ -O0 -fverbose-asm -no-pie -g  -o main main.cpp
```
其中，``-O0``让编译器不做任何优化，``-fverbose-asm``让生成的汇编代码更具有可读性，如果想调试``g++``本身则会有用，``-no-pie``不生成位置无关代码，没有这个会让指令的地址都小于``0x40000``，不方便打断点，详细的介绍参考[这篇博客](https://www.zhihu.com/question/552957041)，``-g``生成调试信息。

用``objdump -d main``查看``main``函数以及``sum``函数的汇编代码如下:
```asm
Disassembly of section .text:

...
0000000000401136 <_Z3sumii>:
  401136:       f3 0f 1e fa             endbr64
  40113a:       55                      push   %rbp
  40113b:       48 89 e5                mov    %rsp,%rbp
  40113e:       89 7d ec                mov    %edi,-0x14(%rbp)
  401141:       89 75 e8                mov    %esi,-0x18(%rbp)
  401144:       8b 55 ec                mov    -0x14(%rbp),%edx
  401147:       8b 45 e8                mov    -0x18(%rbp),%eax
  40114a:       01 d0                   add    %edx,%eax
  40114c:       89 45 fc                mov    %eax,-0x4(%rbp)
  40114f:       8b 45 fc                mov    -0x4(%rbp),%eax
  401152:       5d                      pop    %rbp
  401153:       c3                      retq

...
0000000000401154 <main>:
  401154:       f3 0f 1e fa             endbr64
  401158:       55                      push   %rbp
  401159:       48 89 e5                mov    %rsp,%rbp
  40115c:       48 83 ec 20             sub    $0x20,%rsp
  401160:       89 7d ec                mov    %edi,-0x14(%rbp)
  401163:       48 89 75 e0             mov    %rsi,-0x20(%rbp)
  401167:       be 02 00 00 00          mov    $0x2,%esi
  40116c:       bf 01 00 00 00          mov    $0x1,%edi
  401171:       e8 c0 ff ff ff          callq  401136 <_Z3sumii>
  401176:       89 45 fc                mov    %eax,-0x4(%rbp)
  401179:       8b 45 fc                mov    -0x4(%rbp),%eax
  40117c:       89 c6                   mov    %eax,%esi
  40117e:       48 8d 3d 7f 0e 00 00    lea    0xe7f(%rip),%rdi        # 402004 <_IO_stdin_used+0x4>
  401185:       b8 00 00 00 00          mov    $0x0,%eax
  40118a:       e8 b1 fe ff ff          callq  401040 <printf@plt>
  40118f:       b8 00 00 00 00          mov    $0x0,%eax
  401194:       c9                      leaveq
  401195:       c3                      retq
  401196:       66 2e 0f 1f 84 00 00    nopw   %cs:0x0(%rax,%rax,1)
  40119d:       00 00 00

```

开启GDB，查看``main``函数的汇编代码， 并且在``main``函数的入口处打断点
```bash
$ gdb -q ./main
Reading symbols from ./main...
(gdb) disassemble main
Dump of assembler code for function main(int, char**):
   0x0000000000401154 <+0>:     endbr64
   0x0000000000401158 <+4>:     push   %rbp
   0x0000000000401159 <+5>:     mov    %rsp,%rbp
   0x000000000040115c <+8>:     sub    $0x20,%rsp
   0x0000000000401160 <+12>:    mov    %edi,-0x14(%rbp)
   0x0000000000401163 <+15>:    mov    %rsi,-0x20(%rbp)
   0x0000000000401167 <+19>:    mov    $0x2,%esi
   0x000000000040116c <+24>:    mov    $0x1,%edi
   0x0000000000401171 <+29>:    call   0x401136 <_Z3sumii>
   0x0000000000401176 <+34>:    mov    %eax,-0x4(%rbp)
   0x0000000000401179 <+37>:    mov    -0x4(%rbp),%eax
   0x000000000040117c <+40>:    mov    %eax,%esi
   0x000000000040117e <+42>:    lea    0xe7f(%rip),%rdi        # 0x402004
   0x0000000000401185 <+49>:    mov    $0x0,%eax
   0x000000000040118a <+54>:    call   0x401040 <printf@plt>
   0x000000000040118f <+59>:    mov    $0x0,%eax
   0x0000000000401194 <+64>:    leave
   0x0000000000401195 <+65>:    ret
End of assembler dump.
(gdb) b *0x0401158
Breakpoint 1 at 0x401158: file main.cpp, line 8.
(gdb) r
Starting program: /xxx/main

Breakpoint 1, 0x0000000000401158 in main (argc=0, argv=0x0) at main.cpp:8
8       int main(int argc, char* argv[]){
```

记录此时的``rip``, ``rsp``,``rbp``的寄存器状态:
```bash
(gdb) i r rip rsp rbp
rip            0x401158            0x401158 <main(int, char**)+4>
rsp            0x7fffffffda98      0x7fffffffda98
rbp            0x0                 0x0
```

``rip``,``rsp``,``rbp``的保存的值都是地址，因此这几个寄存器都相当于一个指针，``rip``指向CPU下一步要执行的指令的地址，``rsp``指向了当前函数调用的栈顶，``rbp``未指向任何我们关注的栈和指令，它的值为``0x0``。

现在开始执行第一条指令:
```bash
0x401158 <+4>:     push   %rbp  #保存调用者的rbp寄存器的值
```
这条指令的作用是把栈基地址寄存器``rbp``的值临时保存在``main``函数的栈帧率里，因为``main``函数自身也需要使用``rbp``寄存器来存放自己的栈基地址，而``main``的调用者在调用之前也把它的栈基地址放在了``rbp``里，所以``main``函数需要把``rbp``寄存器里的值先保存起来，等``main``函数执行完成之后返回时再将``rbp``恢复原样。``push``指令执行完之后，再来查看``rip``,``rsp``,``rbp``这几个寄存器的值：
```bash
(gdb) si
(gdb) i r rip rsp rbp
rip            0x401159            0x401159 <main(int, char**)+5>
rsp            0x7fffffffda90      0x7fffffffda90
rbp            0x0
```
对比指令之前的值可以发现，``push``指令会改变``rsp``寄存器的值，``rip``寄存器的值的修改是CPU自动完成的，CPU知道他要执行的每一条的长度，因此可以自动计算出下一条只指令的位置(``rip`` = ``rip`` + 指令长度)。

接着执行第二条指令:
```bash
0x401159 <main(int, char**)+5>  mov    %rsp,%rbp # 调整rbp寄存器,使其指向main函数栈帧的起始位置
```
这条指令的作用是把``rsp``的值拷贝给``rbp``寄存器，让其指向``main``函数栈帧的起始位置，这条指令完成之后，``rsp``和``rbp``寄存器具有相同的值，它们都指向了``main``函数栈帧的起始位置:
```bash
(gdb) si
(gdb) i r rip rsp rbp
rip            0x40115c            0x40115c <main(int, char**)+8>
rsp            0x7fffffffda90      0x7fffffffda90
rbp            0x7fffffffda90      0x7fffffffda90
```
接着执行第3条指令:
```bash
0x40115c <main(int, char**)+8>  sub    $0x20,%rsp #调整rsp寄存器的值,为局部和临时变量预留栈空间
```
这条指令把``rsp``寄存器的值减去``32``，使其指向栈空间中一个更低的位置，通过这部操作给``main``函数的局部临时变量预留了``32``个字节的空间。注意是预留而不是分配，因为栈的分配是操作系统完成的，程序启动的时候操作系统会分配一大块内存用作函数调用栈。程序真正使用了多少栈内存由``rsp``寄存器来确定。

该条指令执行完成之后，``rsp``所指的位置和``rbp``所指的位置这一段区域里的栈内存构成了``main``函数的栈帧，大小是``32``个字节，用来保存``main``函数的局部临时变量。
```bash
(gdb) si
(gdb) i r rip rsp rbp
rip            0x401160            0x401160 <main(int, char**)+12>
rsp            0x7fffffffda70      0x7fffffffda70
rbp            0x7fffffffda90      0x7fffffffda90

```
后面两条指令的执行过程和寄存器状态如下:
```bash
(gdb) i r rbp edi rsi
rbp            0x7fffffffda90      0x7fffffffda90
edi            0x1                 1
rsi            0x7fffffffdb88      140737488345992

0x401160 <main(int, char**)+12> mov    %edi,-0x14(%rbp) #保存main函数的第1个参数
0x401163 <main(int, char**)+15> mov    %rsi,-0x20(%rbp) #保存main函数的第2个参数

(gdb) p argc
$10 = 1
(gdb) p &argc
$11 = (int *) 0x7fffffffda7c #0x7fffffffda7c + 0x14 = 0x7fffffffda90(rbp)
(gdb) x/xw 0x7fffffffda7c
0x7fffffffda7c: 0x00000001
(gdb) p argv[0]
$7 = 0x7fffffffdf72 "/xxx/main"
(gdb) p &argv[0]
$8 = (char **) 0x7fffffffdb88  #rsi
(gdb) x/wx 0x7fffffffda70  #0x7fffffffdf70 + 0x20 = 0x7fffffffdf90(rbp)
0x7fffffffda70: 0xffffdb88
```
这两条指令负责把``main``函数的2个参数保存在栈帧里，这边采用了``rbp``加偏移量的方式来访问栈内存。之所以要保存，是因为调用者在调用``main``函数的时候使用了``edi``和``rsi``这两个寄存器来分别传递``argc``, ``argv``这两个参数，而``main``函数也需要这两个寄存器给``sum``函数传递参数。为了不被覆盖，需要先把这两个参数保存在栈中，然后把传递给``sum``的两个参数放入到这两个寄存器里。

打印相关的变量可以发现: ``p &argc``显示的地址是``0x7fffffffda7c``，而这个地址正好是``0x7fffffffda90(rbp)``减去``0x14``得到的。而这个地址里面的值正好是``0x00000001``，是``edi``通过指令``mov    %edi,-0x14(%rbp)``传进来的。``rsi``寄存器里的值是``0x7fffffffdb88``，它被传到了``0x7fffffffda70``这个地址处，这点可以通过``x/wx 0x7fffffffda70``的结果来验证。而``0x7fffffffda70``这个地址正好是``0x7fffffffda90(rbp)``减去``0x20``得到的。而``argv[0]``本身存储的也是一个地址``0x7fffffffdf72``，它指向了第一个命令行参数``/xxx/main``这个字符串。从绝对值上，``0x7fffffffdf72``比``0x7fffffffdb88``大，这也验证了CSAPP上9.9节插图的里``.text``段在``.data``段下面的结论： 
<div align="center">
<img src=./virtual_space.png>
</div>

接下来两条指令负责为后续的``sum``函数调用准备参数:
```bash
0x401167 <main(int, char**)+19> mov    $0x2,%esi
0x40116c <main(int, char**)+24> mov    $0x1,%edi
```

传递的第1个参数放到了``edi``寄存器里，第2个参数放到了``esi``寄存器里。在gcc中，将第一个参数放到``rdi``，第二个参数放到``rsi``，第三个参数放到``rdx``寄存器里，...这是一个约定。这里传递给``sum``的两个参数分别使用``edi``和``esi``，而不是``rdi``和``rsi``的原因是:在C语言中，``int``是32位的，``rdi``，``rsi``都是64位的，``edi``和``esi``可以分别当成``rdi``和``rsi``的一部分来使用。

参数准备完毕后，开始执行``call``指令调用``sum``函数:
```bash
(gdb) i r rip
rip            0x401171            0x401171 <main(int, char**)+29>
(gdb) i r rsp
rsp            0x7fffffffda70      0x7fffffffda70
0x401171 <main(int, char**)+29> call   0x401136 <_Z3sumii>
(gdb) si
sum (a=32767, b=-9609) at main.cpp:3
(gdb) i r rsp
rsp            0x7fffffffda68      0x7fffffffda68
(gdb) i r rip
rip            0x401136            0x401136 <sum(int, int)>
(gdb) x 0x7fffffffda68
0x7fffffffda68: 0x00401176
```
``call``指令的特点是:在刚开始执行它的时候，``rip``是当前执行的``call``指令的下一条指令，即``rip``寄存器的值是``0x401176``这个地址，但在``call``指令的执行过程中，``call``指令会把当前的``rip``值入栈(具体做法是先减小``rsp``, ``0x7fffffffda70`` -> ``0x7fffffffda68``,然后把``0x401176``这个地址放到``0x7fffffffda68``这个地址处)，然后修改``rip``的值为``call``指令后面的操作数，这里是``0x401136``，即``sum``函数的第一条指令的地址，这样CPU会跳转到``sum``函数里去执行指令。

``main``函数当中执行了调用``sum``函数的``call``指令之后，CPU现在跳转到``sum``函数开始执行:
```bash
0x401136 <_Z3sumii>             endbr64
0x40113a <_Z3sumii+4>           push   %rbp  #保存main函数的rbp的值入栈
0x40113b <_Z3sumii+5>           mov    %rsp,%rbp  #修改当前rbp的值为当前的栈顶
0x40113e <_Z3sumii+8>           mov    %edi,-0x14(%rbp)  #把第1个参数放入临时变量
0x401141 <_Z3sumii+11>          mov    %esi,-0x18(%rbp)  #把第2个参数放入临时变量
0x401144 <_Z3sumii+14>          mov    -0x14(%rbp),%edx  #将第1个临时变量放入到edx当中
0x401147 <_Z3sumii+17>          mov    -0x18(%rbp),%eax  #将第2个临时变量放入到eax当中
0x40114a <_Z3sumii+20>          add    %edx,%eax  # 进行加法计算, 结果保存在eax当中
0x40114c <_Z3sumii+22>          mov    %eax,-0x4(%rbp)  #将eax的值保存到临时变量中
0x40114f <_Z3sumii+25>          mov    -0x4(%rbp),%eax  #将临时变量的值放入到eax寄存器当中
0x401152 <_Z3sumii+28>          pop    %rbp  # 出栈, 恢复main函数的rbp的值
0x401153 <_Z3sumii+29>          ret  #函数返回
```
``sum``函数并没有像``main``函数一样通过调整``rsp``寄存器的值来给``sum``函数预留局部临时变量，因为``sum``函数里面的``call``指令会自动调整``rsp``的值以便将``sum``函数的返回地址(``0x401176``)保存到``rsp``所指定的栈内存位置上。如果``main``函数不调整``rsp``的值，则``call``指令保存函数返回地址会覆盖局部临时变量的值，而``sum``函数中没有任何指令会自动调整``rsp``寄存器来保存数据到栈上，所以不需要调整``rsp``寄存器。

观察``push   %rbp ``指令执行前后``rbp``和``rsp``寄存器的变化:
```bash
(gdb) i r rbp rsp
rbp            0x7fffffffda90      0x7fffffffda90
rsp            0x7fffffffda68      0x7fffffffda68
(gdb) si

No more reverse-execution history.
0x000000000040113b in sum (a=32767, b=-9609) at main.cpp:3
(gdb) i r rbp rsp
rbp            0x7fffffffda90      0x7fffffffda90
rsp            0x7fffffffda60      0x7fffffffda60
(gdb) x/wx 0x7fffffffda60
0x7fffffffda60: 0xffffda90
```
可见``push   %rbp ``指令的确将``rbp``的值保存在了栈上(``rsp``里保存的地址处)

继续执行``sum``函数里的指令，直到``ret``指令处。观察``ret``指令执行前后``rip``,``rbp``,``rsp``寄存器的变化:
```bash
0x0000000000401153 in sum (a=1, b=2) at main.cpp:6
(gdb) i r rip rbp rsp
rip            0x401153            0x401153 <sum(int, int)+29>
rbp            0x7fffffffda90      0x7fffffffda90
rsp            0x7fffffffda68      0x7fffffffda68
(gdb) x/xw 0x7fffffffda68
0x7fffffffda68: 0x00401176
(gdb) si

No more reverse-execution history.
0x0000000000401176 in main (argc=1, argv=0x7fffffffdb88) at main.cpp:10
(gdb) i r rip rbp rsp
rip            0x401176            0x401176 <main(int, char**)+34>
rbp            0x7fffffffda90      0x7fffffffda90
rsp            0x7fffffffda70      0x7fffffffda70
```
可见``ret``指令把``rsp``指向的内存单元``0x7fffffffda68``当中的``0x00401176``取出来赋给``rip``寄存器，同时``rsp``加上2。这样``rip``寄存器中的值就变成了``main``函数里调用``sum``的``call``指令的下一条指令，于是程序执行流程返回到``main``函数中继续执行。

继续执行``main``函数中的指令:
```bash
0x401176 <main(int, char**)+34> mov    %eax,-0x4(%rbp) #把sum函数的返回值赋给变量n
```
该指令是把``rax``寄存器当中的值(``sum``函数的返回值)放入到``rbp-4``所指的内存，也就是变量``n``所在的位置。后面几条指令:
```bash
0x401179 <main(int, char**)+37> mov    -0x4(%rbp),%eax  #将变量n的值放入eax
0x40117c <main(int, char**)+40> mov    %eax,%esi  #函数printf的第2个参数
0x40117e <main(int, char**)+42> lea    0xe7f(%rip),%rdi  #0x402004, 函数printf的第1个参数
0x401185 <main(int, char**)+49> mov    $0x0,%eax  #函数printf返回值
0x40118a <main(int, char**)+54> call   0x401040 <printf@plt>  #函数调用
0x40118f <main(int, char**)+59> mov    $0x0,%eax  #main函数返回值
0x401194 <main(int, char**)+64> leave  
0x401195 <main(int, char**)+65> ret
```
这边重点关注一下``lea    0xe7f(%rip),%rdi``这个指令，这个是为了给``printf``函数传递第一个参数，是一个字符串，``lea``指令操作的是一个地址，是一个相对于``rip``寄存器保存指令的下一条指令地址的寻址方式，从汇编结果可以看出，此时的``rip``的值是``0x40117e``，它的下一条指令地址是``0x401185``，加上``0xe7f``，得到的值是``0x402004``，而地址``0x402004``处保存的刚好是``"n : %d\n"``这个字符串，并且这个``0x402004``地址是可以在连接时就确定好的，无需运行时动态计算。
```bash
(gdb) i r rip
rip            0x40117e            0x40117e <main(int, char**)+42>
(gdb) x/s 0x402004
0x402004:       "n : %d\n"
```
还有一个是``levae``指令:
```bash
0x401194 <main(int, char**)+64> leave

(gdb) i r rbp rsp
rbp            0x7fffffffda90      0x7fffffffda90
rsp            0x7fffffffda70      0x7fffffffda70
(gdb) x/wx 0x7fffffffda90
0x7fffffffda90: 0x00000000
(gdb) si 
(gdb) i r rbp rsp
rbp            0x0                 0x0
rsp            0x7fffffffda98      0x7fffffffda98
```
``leave``指令会把``rbp``寄存器的值复制给``rsp``，这样``rsp``就指向了``rbp``的栈单元。然后将该内存单元(``0x7fffffffda90``)的当中的值(``0x0``)pop出来给``rbp``,这样``rbp``和``rsp``就恢复到了刚进入``main``函数时的状态了。

到此，``main``函数就只剩下``ret``指令了，该条指令已经在前面``sum``函数返回时分析过了，这条指令完成之后就会返回到调用``main``函数的函数中继续执行了。
